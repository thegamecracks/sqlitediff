%import common (CNAME, SQL_COMMENT, WS)
%ignore WS
%ignore SQL_COMMENT

start: statement+

statement: create_table (";" statement)* ";"?

create_table: "CREATE"i "TABLE"i name "(" columns table_constraints? ")" table_options?
columns: column ("," column)*
table_constraints: ("," table_constraint)+
table_options: table_option ("," table_option)*

name: CNAME
    | /"[^"]+"/
    | /'[^']+'/

column: name type? column_constraint*
type: name

column_constraint: CONSTRAINT name
                  | PRIMARY KEY unknown_token*
                  | UNIQUE unknown_token*
                  | CHECK unknown_token+
                  | REFERENCES unknown_token+
                  | NOT NULL unknown_token*
                  | DEFAULT unknown_token+
                  | COLLATE unknown_token+
                  | GENERATED ALWAYS unknown_token+
                  | AS unknown_token+
 table_constraint: CONSTRAINT name
                  | PRIMARY KEY unknown_token+
                  | UNIQUE unknown_token*
                  | CHECK unknown_token+
                  | FOREIGN KEY unknown_token+
table_option: unknown_token+
?!unknown_token: name | "(" ( unknown_token | "," )+ ")"

ALWAYS: "ALWAYS"i
AS: "AS"i
CHECK: "CHECK"i
COLLATE: "COLLATE"i
CONSTRAINT: "CONSTRAINT"i
DEFAULT: "DEFAULT"i
FOREIGN: "FOREIGN"i
GENERATED: "GENERATED"i
KEY: "KEY"i
NOT: "NOT"i
NULL: "NULL"i
PRIMARY: "PRIMARY"i
REFERENCES: "REFERENCES"i
UNIQUE: "UNIQUE"i
